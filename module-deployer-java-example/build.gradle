plugins {
    id "java"
    id "application"
    id "idea"
}

mainClassName = 'example.Launcher'

repositories {
    jcenter()
}

def downloadLibFromUrl(String libSaveDir, String libName, String libUrl) {
    def folder = new File(libSaveDir)
    if (!folder.exists()) {
        // creates the missing folders tree
        folder.mkdirs()
    }
    def file = new File("$libSaveDir/$libName")
    if (!file.exists()) {
        // downloads the lib from the specified url
        ant.get(src: libUrl, dest: file)
    }
    // adds the lib to dependencies
    getDependencies().add('compile', fileTree(dir: libSaveDir, include: libName))
}

dependencies {
    def libSaveDir = "${System.properties['user.home']}/.gradle/caches/modules-2/files-2.1"
    def version = '1.1.0'
    def libName = "multi-module-deployer-${version}.jar"
    def url = "https://github.com/FlamingTuri/multi-module-deployer/releases/download/v$version/$libName"
    downloadLibFromUrl(libSaveDir, libName, url)
}
